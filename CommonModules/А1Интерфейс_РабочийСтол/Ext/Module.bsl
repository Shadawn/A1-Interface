Функция НастройкиМеханизма() Экспорт
	Настройки = А1Э_Механизмы.НовыйНастройкиМеханизма();
	
	Настройки.Обработчики.Вставить("ФормаПриСозданииОбъектов", Истина);
	Настройки.Обработчики.Вставить("ФормаПриСозданииНаСервере", Истина);
	
	Настройки.ПорядокВыполнения = -1000000000;
	
	Возврат Настройки;
КонецФункции

#Если НЕ Клиент Тогда
	
	Функция ФормаПриСозданииОбъектов(Форма, Отказ, МассивОписаний) Экспорт
		А1Э_Формы.ДобавитьОписаниеГоризонтальнойГруппы(МассивОписаний, "ГруппаОбщая");
		
		А1Э_Формы.ДобавитьОписаниеВертикальнойГруппы(МассивОписаний, "ГруппаЛево", , "ГруппаОбщая");
		А1Э_Формы.ДобавитьОписаниеВертикальнойГруппы(МассивОписаний, "ГруппаПраво", , "ГруппаОбщая");
		
		А1Э_Формы.ДобавитьОписаниеВертикальнойГруппы(МассивОписаний, "ГруппаВерхЛево", , "ГруппаЛево");
		А1Э_Формы.ДобавитьОписаниеВертикальнойГруппы(МассивОписаний, "ГруппаНизЛево", , "ГруппаЛево");
		А1Э_Формы.ДобавитьОписаниеВертикальнойГруппы(МассивОписаний, "ГруппаВерхПраво", , "ГруппаПраво");
		А1Э_Формы.ДобавитьОписаниеВертикальнойГруппы(МассивОписаний, "ГруппаНизПраво", , "ГруппаПраво");
		
		А1Интерфейс.А1БК_ГруппаСКолонками().ДобавитьОписание(МассивОписаний, "ГруппаМоиДела", 2, "Мои дела", "ГруппаВерхЛево");
		А1Интерфейс.А1БК_ГруппаСКолонками().ДобавитьОписание(МассивОписаний, "ГруппаСправочники", 2, "Справочники", "ГруппаВерхЛево");
		А1Интерфейс.А1БК_ГруппаСКолонками().ДобавитьОписание(МассивОписаний, "ГруппаДокументы", 2, "Документы", "ГруппаВерхЛево");
		А1Интерфейс.А1БК_ГруппаСКолонками().ДобавитьОписание(МассивОписаний, "ГруппаОтчеты", 2, "Отчеты", "ГруппаВерхЛево");
		
		ДобавитьОписаниеКнопкиОткрытияФормы(МассивОписаний, "ОбщаяФорма.А1Интерфейс_ПанельОтчетов", "Отчеты", "ГруппаМоиДела");
		ДобавитьОписаниеКнопкиОткрытияФормы(МассивОписаний, "ОбщаяФорма.А1Интерфейс_Настройки", "Настройки", "ГруппаМоиДела");
		
		А1Э_Механизмы.ВыполнитьМеханизмыОбработчика("А1Интерфейс_РабочийСтол__ПриСозданииОбъектов", Форма, Отказ, МассивОписаний);
	КонецФункции
	
	Функция ФормаПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
		А1Э_Механизмы.ВыполнитьМеханизмыОбработчика("А1Интерфейс_РабочийСтол__ПриСозданииНаСервере", Форма, Отказ, СтандартнаяОбработка);
	КонецФункции
	
#КонецЕсли

#Область Компоненты
#Если НЕ Клиент Тогда
	
	Функция ДобавитьОписаниеПроизвольнойКнопки(МассивОписаний, Имя, Действие, Заголовок = Неопределено, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт 
		РабочиеПараметры = А1Э_Структуры.СкопироватьВШаблон(Параметры,
		"Ширина", 30,
		);
		А1Э_Формы.ДобавитьОписаниеКомандыИКнопки2(МассивОписаний, Имя, Действие, Заголовок, РодительЭлемента, ПередЭлементом, РабочиеПараметры, Действия); 
	КонецФункции
	
	Функция ДобавитьОписаниеКнопкиОткрытияФормы(МассивОписаний, ИмяОткрываемойФормы, Заголовок = Неопределено, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено, Действия = Неопределено) Экспорт 
		ВтораяТочка = СтрНайти(ИмяОткрываемойФормы, ".", , , 2);
		Если ВтораяТочка = 0 Тогда
			ИмяМетаданных = ИмяОткрываемойФормы;
		Иначе
			ИмяМетаданных = Лев(ИмяОткрываемойФормы, ВтораяТочка - 1);
		КонецЕсли;
		Если НЕ ПравоДоступа("Просмотр", А1Э_Метаданные.ОбъектМетаданных(ИмяМетаданных)) Тогда Возврат Неопределено; КонецЕсли;
		
		ДобавитьОписаниеПроизвольнойКнопки(МассивОписаний, СтрЗаменить(ИмяОткрываемойФормы, ".", "___"), ИмяМодуля() + ".ОткрытьФормуСРабочегоСтола", Заголовок, РодительЭлемента, ПередЭлементом, Параметры, Действия);   
		
	КонецФункции 
	
	Функция ДобавитьОписаниеНабораКнопокСписка(МассивОписаний, ИмяМетаданных, РодительЭлемента = Неопределено, ПередЭлементом = Неопределено, Параметры = Неопределено) Экспорт
		Если НЕ А1Э_Доступы.ЕстьПраво("Просмотр", А1Э_Метаданные.ОбъектМетаданных(ИмяМетаданных)) Тогда Возврат Неопределено; КонецЕсли; 
		
		РабочиеПараметры = А1Э_Структуры.СкопироватьВШаблон(Параметры,
		"Ширина", 26,
		);
		А1Интерфейс.А1БК_НаборКнопокСписка().ДобавитьОписание(МассивОписаний, ИмяМетаданных, РодительЭлемента, ПередЭлементом, РабочиеПараметры);
	КонецФункции 
	
#КонецЕсли
#Если Клиент Тогда
	
	Функция ОткрытьФормуСРабочегоСтола(Форма, Команда) Экспорт
		ИмяФормы = СтрЗаменить(Команда.Имя, "___", ".");
		ОткрытьФорму(ИмяФормы);
	КонецФункции 
	
#КонецЕсли
 
#КонецОбласти 

Функция ИмяМодуля() Экспорт
	Возврат "А1Интерфейс_РабочийСтол";	
КонецФункции 